buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://kotlin.bintray.com/kotlin-js-wrappers' }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

final props = new Properties()
file("$rootDir/.env").with { file ->
    if (file.canRead()) {
        file.withInputStream { is -> props.load(is) }
    }
}
ext {
// note that system properties should override the .env file ones.
    customizedEnvironment = [*: props, *: System.getenv()]
}

task cleanOutputProject(type: Delete) {
    delete "out"
    delete "build"
    delete "production"
}

task clean {}
clean.dependsOn(cleanOutputProject)
